PHDRS
{
    text PT_LOAD FLAGS(5); /* RX: Read + Execute */
    data PT_LOAD FLAGS(6); /* RW: Read + Write */
    bss  PT_LOAD FLAGS(6);  /* RW: Read + Write */
}

/* Entry Point */
ENTRY(Reset_Handler)

/* value of stack pointer */
_estack = ORIGIN(SRAM) + LENGTH(SRAM);

/* Memories definition */
MEMORY
{
    FLASH(rx): ORIGIN = 0x08000000, LENGTH = 256K
    SRAM(rw) : ORIGIN = 0x20000000, LENGTH = 64K
}

SECTIONS
{
    .isr_vector : { KEEP(*(.isr_vector)); . = ALIGN(4); } > FLASH :text
    .text : {
        *(.text)
        *(.text.*)
        *(.init)
        *(.fini)
        *(.rodata)
        *(.rodata.*)
        . = ALIGN(4);
        _etext = .;
    } > FLASH :text

    _la_data = LOADADDR(.data);

    .data : {
        _sdata = .;
        *(.data)
        *(.data.*)
        . = ALIGN(4);
        _edata = .;
    } > SRAM AT > FLASH :data

    .bss : {
        _sbss = .;
        *(.bss)
        *(.bss.*)
        *(COMMON)
        . = ALIGN(4);
        _ebss = .;
        . = ALIGN(4);
        _end = .;
    } > SRAM :bss
}
